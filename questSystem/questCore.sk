function questNew(player: player, string: string):

    send "" to {_player}
    send "&a[QUEST] » &fYou have started the quest &a%{_string}%&f. Check your Quest Book to gain additional information how to finish this quest." to {_player}
    send "" to {_player}

function questComplete(player: player, strings: strings):

    send "" to {_player}
    send "&a[QUEST] » &fYou have successfully completed the quest &a%{_strings::1}%&f. As a reward for completing the quest you gained %{_strings::2}%." to {_player}
    send "" to {_player}

function questTalk(player: player):

    set {_random} to random integer between 1 and 3
    if {_random} is 1:
        play sound "ENTITY_VILLAGER_AMBIENT" at volume 0.2 to {_player}
    else if {_random} is 2:
        play sound "ENTITY_VILLAGER_YES" at volume 0.2 to {_player}
    else if {_random} is 3:
        play sound "ENTITY_VILLAGER_TRADE" at volume 0.2 to {_player}

function questBookGetQuest(player: player, quest: string) :: item:

    set {_u} to uuid of {_player}
   
    if {quests::%{_quest}%::completed::%{_u}%} is false:

        set {_dialougeNumber} to 1
        set {_addedDialouge} to 0
        set {_book} to book named "&a%{quests::%{_quest}%::name}%" with lore "&7%{quests::%{_quest}%::objective}%", "" and "&7&nDialouge:"

        loop {quests::%{_quest}%::dialouge::*}:
           
            if {quests::%{_quest}%::interaction::%{_u}%::%{_dialougeNumber}%} is true:
                set {_split::*} to split uncolored loop-value at ": "

                set {_add::*} to uncolored split {_split::2} at " "
                set {_add} to ""
                set {_loopIndex} to 1

                while true:
                    if {_add::%{_loopIndex}%} is set:
                        set {_add} to "%{_add}% %{_add::%{_loopIndex}%}%"
                        if {_loopIndex} is 9:
                            add "&8[%{_dialougeNumber}%.]&7%{_add}%" to lore of {_book}
                            set {_add} to ""
                        else if {_loopIndex} is 18 or 27:
                            add "&7%{_add}%" to lore of {_book}
                            set {_add} to ""
                    else if {_loopIndex} <= 8:
                        add "&8[%{_dialougeNumber}%.]&7%{_add}%" to lore of {_book}
                        stop loop
                    else if {_loopIndex} >= 10: 
                        if {_add} is not "":
                            add "&7%{_add}%" to lore of {_book}
                        stop loop           
                    else:
                        stop loop

                    add 1 to {_loopIndex}

                add 1 to {_addedDialouge}

            add 1 to {_dialougeNumber}

        add "" and "&7&nObjective:" to lore of {_book}
        
        loop {quests::%{_quest}%::quota::*}:
            add "&8- &7%loop-value%" to lore of {_book}
        
        add "" and "&f[%{quests::%{_quest}%::coords}%]" to lore of {_book}

        if {_addedDialouge} is 0:
            add "&cYou haven't talked to this NPC yet." to lore of {_book}
        
        else:
            add "&fThere's more dialouge to unlock with this NPC." to lore of {_book}

        return {_book}
    else if {quests::%{_quest}%::talked::%{_u}%} is not true:

        if {quests::%{_quest}%::name} is set:
            set {_notTalked} to gray dye named "&f%{quests::%{_quest}%::name}%" with lore "&7%{quests::%{_quest}%::start}%"
            add "" and "&f[%{quests::%{_quest}%::coords}%]" to lore of {_notTalked}
            return {_notTalked} 
    
    else:
        return air

on rightclick:

    if uncolored name of event-item is "Quest Book":
    
        set metadata tag "quest" of player to chest with 6 row named "&8Quest Book"
        
        set {_quests::*} to ("firstSlay", "animalFood", "rootedChallenge", "mysteryItem", "luckyCarrot", "conjecture", "healthyCure", "botany", "shadeMatter", "lastBreath") # add new quests

        set {_borderIndex} to 0
        set {_questIndex} to 1
        set {_placeIndex} to 0
        
        loop 54 times:

            if {_borderIndex} is 7, 8, 16, 17, 25, 26, 34, 35, 43, 44, 52 or 53:
                if {_borderIndex} is 44:
                    set slot {_borderIndex} of metadata tag "quest" of player to barrier named "&cClose"
                else if {_borderIndex} is 35:
                
                    set {_questProgress} to paper named "&aQuest Progress"
                    set {_completed} to 0
                    set {_bar} to ""

                    loop {_quests::*}:
                        if {quests::%loop-value-2%::completed::%player's uuid%} is true:
                            add 1 to {_completed}

                    add "&7You currently have finished %{_completed}%/%size of {_quests::*}%", "&7of all the quests in the game." and "" to lore of {_questProgress}

                    loop 10 times:
                        if {_completed} >= (size of {_quests::*} / 10) * loop-number-2:
                            set {_bar} to "%{_bar}%&f▌"
                        else:
                            set {_bar} to "%{_bar}%&7▌"

                    add "%{_bar}%" to lore of {_questProgress}
                    set slot {_borderIndex} of metadata tag "quest" of player to {_questProgress}
                
                else:
                    set slot {_borderIndex} of metadata tag "quest" of player to black stained glass pane named "&8Border"
            
            else:
                if {_placeIndex} is not 7 or 8:
                    if questBookGetQuest(player, {_quests::%{_questIndex}%}) is book:
                        set slot {_placeIndex} of metadata tag "quest" of player to questBookGetQuest(player, {_quests::%{_questIndex}%})
                        add 1 to {_placeIndex}
                        add 1 to {_questIndex}
                    else if questBookGetQuest(player, {_quests::%{_questIndex}%}) is gray dye:
                        set slot {_placeIndex} of metadata tag "quest" of player to questBookGetQuest(player, {_quests::%{_questIndex}%})
                        add 1 to {_placeIndex}
                        add 1 to {_questIndex}
                    else if questBookGetQuest(player, {_quests::%{_questIndex}%}) is air:
                        add 1 to {_questIndex}
                else:
                    add 1 to {_placeIndex}

            add 1 to {_borderIndex}

        open (metadata tag "quest" of player) to player

on inventory click:

    if event-inventory = (metadata tag "quest" of player): 
        cancel event

        if index of event-slot is 44:
            close player's inventory

command /quest [<text>] [<text>] [<text>] [<text>] [<text>]:
    permission: op
    permission message: Unknown command. Type "/help" for help.
    trigger:

        if arg-1 is set:

            if arg-1 is "user":
     
                if arg-2 is set:

                    if arg-2 parsed as offline player is offline player:

                        set {_player} to arg-2 parsed as offline player
                        set {_u} to uuid of {_player}

                        if {level::%{_u}%} is set:

                            if arg-3 is set:

                                set {_quests::*} to ("firstSlay", "animalFood", "rootedChallenge", "mysteryItem", "luckyCarrot", "conjecture", "healthyCure", "botany", "shadeMatter", "lastBreath") # add new quests

                                if arg-3 is "clear":
                                    send "&a[QUEST] » &fYou cleared all of &a%arg-2%&f's quiz progress."
                                    loop {_quests::*}:
                                        clear {quests::%loop-value%::talked::%{_u}%}
                                        clear {quests::%loop-value%::completed::%{_u}%}
                                        clear {quests::%loop-value%::interaction::%{_u}%::*} 
                                else if arg-3 is "list":
                                    send "&a[QUEST] » &fShowing quest status for &a%arg-2%&f..."
                                    loop {_quests::*}:
                                        if {quests::%loop-value%::completed::%{_u}%} is true:
                                            set {_completed::%loop-value%} to "&f%loop-value% - &acompleted"
                                        else:
                                            set {_completed::%loop-value%} to "&f%loop-value% - &cnot completed"
                                        send "&a[QUEST] » &f%{_completed::%loop-value%}%&f."
                                else if arg-3 is "set":

                                    if arg-4 is set:

                                        loop {_quests::*}:
                                            if arg-4 is loop-value:
                                                set {_quest} to true
                                                stop loop

                                        if {_quest} is true:

                                            if arg-5 is set:

                                                if arg-5 is "true":
                                                    send "&a[QUEST] » &fYou magically finished the quest &a%arg-4% &ffor &a%arg-2%.&f"
                                                    set {quests::%arg-4%::interaction::%{_u}%::*} to (true, true, true, true, false)
                                                    set {quests::%arg-4%::talked::%{_u}%} to true
                                                    set {quests::%arg-4%::completed::%{_u}%} to true
                                                else if arg-5 is "false":
                                                    send "&a[QUEST] » &fYou cleared the progress of quest &a%arg-4% &ffor &a%arg-2%.&f"
                                                    clear {quests::%arg-4%::talked::%{_u}%}
                                                    clear {quests::%arg-4%::completed::%{_u}%}
                                                    clear {quests::%arg-4%::interaction::%{_u}%::*}
                                                else:
                                                    send "&a[QUEST] » &cPlease specify true or false."

                                            else:
                                                send "&a[QUEST] » &cThis command option does not exist."

                                        else:
                                            send "&a[QUEST] » &cThis quest does not exist."

                                    else:
                                        send "&a[QUEST] » &cThis command option does not exist."
                                else:
                                    send "&a[QUEST] » &cThis command option does not exist."  

                            else:
                                send "&a[QUEST] » &cPlease specify a command option."

                        else:
                            send "&a[QUEST] » &cThis player has never been on the server."

                    else:
                        send "&a[QUEST] » &cThis player does not exist."

                else:
                    send "&a[QUEST] » &cPlease specify a player."  

            else:
                send "&a[QUEST] » &cThis command option does not exist."

        else:
            send "&a[QUEST] » &cPlease specify a command option."

on tab complete of "/quest":

    if tab arg-1 is not "user": # add new options  
        clear {_tabCompletion::*}
        set {_tabCompletion::*} to ("user") # add new options
        set tab completions for position 1 to {_tabCompletion::*}

    if tab arg-1 is "user":
        clear {_tabCompletion::*}
        loop players:
            add loop-value to {_tabCompletion::*}
            set tab completions for position 2 to {_tabCompletion::*}

    set {_user} to tab arg-2 parsed as offline player
    set {_u} to uuid of {_user}
    if {level::%{_u}%} is set:

        if tab arg-3 is not "set" or "clear" or "list": # add new options 
            clear {_tabCompletion::*}
            set {_tabCompletion::*} to ("clear", "set", "list") # add new options 
            set tab completions for position 3 to {_tabCompletion::*}

    set {_quests::*} to ("firstSlay", "animalFood", "rootedChallenge", "mysteryItem", "luckyCarrot", "conjecture", "healthyCure", "botany", "shadeMatter", "lastBreath") # add new quests 
    loop {_quests::*}:
        if tab arg-4 is loop-value:
            set {_quest} to true

    if tab arg-3 is "set":
        set tab completions for position 4 to {_quests::*}
    
    if {_quest} is true:
        set tab completions for position 5 to "true" and "false"